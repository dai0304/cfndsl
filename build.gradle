apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'

ext.artifactId = 'cfndsl'
ext.defaultEncoding = 'UTF-8'
ext {
	// test dependencies
	junitVersion = "4.11"
	hamcrestVersion = "1.3"
	mockitoCoreVersion = "1.9.5"
	
	// loggers
	slf4jVersion = "1.7.2"
	logbackVersion = "1.0.11"
	
	// other
	guavaVersion = "15.0"
	baseunitsVersion = "2.11"
	awsJavaSdkVersion = "1.6.8"
}

defaultTasks 'clean', 'jar'

sourceCompatibility = targetCompatibility = 1.7
tasks.withType(AbstractCompile) each {
	it.options.encoding = ext.defaultEncoding
}
compileJava {
	options.compilerArgs << '-Xlint:deprecation'
}
repositories {
	mavenCentral()
	maven {
		// for baseunits
		url "http://maven.xet.jp/release"
	}
	maven {
		// for cmtest
		url 'http://public-maven.classmethod.info/release' 
	}
}

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.2.0'
	
	// testing
	compile "junit:junit:$junitVersion"
	compile "org.hamcrest:hamcrest-library:$hamcrestVersion"
	compile "org.mockito:mockito-core:$mockitoCoreVersion"

	// loggers
	compile "org.slf4j:slf4j-api:$slf4jVersion"
	compile "org.slf4j:jul-to-slf4j:$slf4jVersion"
	compile "ch.qos.logback:logback-classic:$logbackVersion"
	
	// others
	compile "jp.xet:baseunits:$baseunitsVersion"
	compile "com.amazonaws:aws-java-sdk:$awsJavaSdkVersion"
}

// ======== ソースバンドル・Javadocバンドルの作成 ========

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.8'
}
